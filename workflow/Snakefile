from glob import glob

datafiles = glob("raw_data/**/data.txt", recursive=True)

analysis_plots = "assets/plots/"

all_datafiles = list(
    map(
        lambda s: s.replace("raw_data", "intermediary_data/analysis").replace(
            "txt", "json"
        ),
        datafiles,
    )
)

all_rw_files = list(
    map(
        lambda s: s.replace("raw_data", "intermediary_data/reweights") + ".txt",
        glob("raw_data/L*"),
    )
)

print(all_datafiles)

"""
Rule for fast analysis plots
with no reweighting.

"""


rule plots:
    input:
        "assets/plots/PolyakovLoop.pdf",
        "assets/plots/PolyakovSusceptibility.pdf",
        "assets/plots/PolyakovBinder.pdf",


rule reweighting_plots:
    input:
        data=datafiles,
        src="src/reweighting_plots.py",
        tools="src/analysis_tools.py",
    output:
        polyakov=analysis_plots + "reweighted_polyakov.pdf",
        susceptibility=analysis_plots + "reweighted_susceptibility.pdf",
        binder=analysis_plots + "reweighted_binder.pdf",
    shell:
        "python {input.src} "
        "--polyakov_plot {output.polyakov} "
        "--susceptibility_plot {output.susceptibility} "
        "--binder_plot {output.binder} "


rule full_plots:
    input:
        script="src/plot_reweights.py",
        rwfiles=all_rw_files,
        datafiles=all_datafiles,
    output:
        "assets/plots/{observable}.pdf",
    shell:
        "python {input.script} "
        "--rwfiles {input.rwfiles} "
        "--datafiles {input.datafiles} "
        "--observable {wildcards.observable} "
        "--output {output}"


rule quick_analysis:
    input:
        script="src/quick_analysis.py",
        file=lambda wc: f"raw_data/L{wc.L}/B{wc.B}/data.txt",
    output:
        "intermediary_data/analysis/L{L}/B{B}/data.json",
    shell:
        "python {input.script} --file {input.file} --output {output}"


rule boostrap_rw:
    input:
        files=lambda wc: glob(f"raw_data/L{wc.L}/**/data.txt", recursive=True),
        script="src/simple_rw.py",
    output:
        "intermediary_data/reweights/L{L}.txt",
    shell:
        "python {input.script} "
        "--files {input.files} > {output}"


rule plaquette:
    input:
        data=datafiles,
        src="src/plaquette.py",
    output:
        polyakov=analysis_plots + "plaquette.pdf",
        susceptibility=analysis_plots + "plaquette_susceptibility.pdf",
        binder=analysis_plots + "plaquette_binder.pdf",
    shell:
        "python {input.src} "
        "--plaquette_plot {output.polyakov} "
        "--susceptibility_plot {output.susceptibility} "
        "--binder_plot {output.binder} "


rule analysis_plots:
    input:
        data=datafiles,
        src="src/analysis_plots.py",
        tools="src/analysis_tools.py",
    output:
        polyakov=analysis_plots + "polyakov.pdf",
        susceptibility=analysis_plots + "susceptibility.pdf",
        binder=analysis_plots + "binder.pdf",
    shell:
        "python {input.src} "
        "--polyakov_plot {output.polyakov} "
        "--susceptibility_plot {output.susceptibility} "
        "--binder_plot {output.binder} "


rule clean:
    shell:
        "rm -rf assets/"
